#! /usr/bin/perl



use strict;
use Getopt::Long;
use DBI;

my $name = 'mysql-backup';
my $version = '0.20180303';

my $lockfile = "/tmp/$name.lock";
my $help = undef;
my $help_install = undef;
my $mysqldump = 'mysqldump';
my $defaults_file = $ENV{HOME}.'/.my.cnf';
my $no_defaults_file = undef;
my $mysql_password = undef;
my $mysql_user = undef;
my $mysql_host = undef;
my $backups_dir = "/home/$name/";
my @mysqldump_options = (undef);
my $no_log = undef;
my $log_dir = "/var/log/$name/";
my $label = undef;
my $retention = undef;
my $tar = 'tar -cvf';
my $no_tar = undef;
my @dbs_to_backup;
my $dbs_to_backup_string;
my @exclude_dbs;
my $gzip_pipe = '| gzip -c';

my $backup_tar_file_suffix='_'.$name.'.tar';

GetOptions(
	"mysqldump=s" => \$mysqldump,
	"lockfile=s" => \$lockfile,
	"backups-dir=s" => \$backups_dir,
	"defaults-file=s" => \$defaults_file,
	"no-defaults-file" => \$no_defaults_file,
	"username=s" => \$mysql_user,
	"password=s" => \$mysql_password,
	"hostname=s" => \$mysql_host,
	"mysqldump-option=s" => \@mysqldump_options,
	"help|h" => \$help,
	"log-dir=s" => \$log_dir,
	"no-log" => \$no_log,
	"help-install" => \$help_install,
	"help" => \$help,
	"label=s" => \$label,
	"max-days=i" => \$retention,
	"tar=s" => \$tar,
	"no-tar" => \$no_tar,
	"databases=s" => \$dbs_to_backup_string,
	"exclude=s" => \@exclude_dbs,

) or abort ("Failed to parse command line options", 1);

help_install() if $help_install;
usage() if $help;

write_lock($lockfile);
$SIG{INT} = sub{ unlock($lockfile) and abort("Ctrl-C caught", 1); };

$tar = undef if $no_tar;

# By default we'll log to a logfile that's deduced according to the label 
# (which by convention is the period: daily, weekly, monthly etc.) but if
# --no-log is passed, instead it'll go to stdout (and probably be sent to
# a file somewhere):

my $log_fh = *STDOUT;
my $log_path;
if($log_dir && !$no_log){
	unless (-d $log_dir){ mkdir($log_dir) or abort("Failed to create logs dir '$log_dir' : $!");}
	($log_fh,$log_path)= open_logfile($log_dir, $label);
}

if( !-f $defaults_file and !$no_defaults_file){
	abort("Defaults file '$defaults_file' not found and --no-defults-file not set");
}

# mysqldump requires that --defaults-file be the first option if it's 
# used, so we unshift that to the list of options grabbed from the args.

unshift(@mysqldump_options, "--defaults-file=$defaults_file") unless ($no_defaults_file);
push(@mysqldump_options, "--user=$mysql_user") if $mysql_user;
push(@mysqldump_options, "--password=$mysql_password") if $mysql_password;
push(@mysqldump_options, "--host=$mysql_host") if $mysql_host;

my ($dsn,$dbh);
if($defaults_file and !$no_defaults_file){
	$dsn = "DBI:mysql:mysql;mysql_read_default_file=$defaults_file";
	$dbh = DBI->connect($dsn);
}elsif($mysql_host){
	$dsn = "DBI:mysql:mysql:$mysql_host";
	$dbh = DBI->connect($dsn, $mysql_user, $mysql_password);
}elsif($mysql_user){
	$dsn = "DBI:mysql:mysql";
	$dbh = DBI->connect($dsn, $mysql_user, $mysql_password);
}else{
	abort ("Couldn't find credentials with which to connect to the db");
}

info("Connected to database OK; dsn: $dsn");

# $backup_dir is the directory into which we're going to write this backup
# $backups_dir is its parent; the directory in which backups are kept.

my $backup_dirname = get_backup_dirname();
mkdir($backups_dir) or abort("Failed to create backups dir '$backups_dir': $!") unless -d $backups_dir;
$backups_dir = $backups_dir.'/'.$label.'/' if $label;
$backups_dir =~ s#//#/#g;
mkdir($backups_dir) or abort("Failed to create backups dir '$backups_dir': $!") unless -d $backups_dir;

my $backup_dir = $backups_dir.$backup_dirname.'/';
$backup_dir =~ s#//#/#g;

if( !-d $backup_dir){
	mkdir($backup_dir) or abort("Failed to create backup dir '$backup_dir' : $!");
}

info("Backup dir: $backup_dir");

info("DBs to backup from commandline: $dbs_to_backup_string");
my @dbs_to_backup;
if($dbs_to_backup_string){
	@dbs_to_backup = split(m/,/, $dbs_to_backup_string);
}else{
	my $sth = $dbh->prepare("show databases");
	$sth->execute();
	while (my @row = $sth->fetchrow_array){
		push(@dbs_to_backup, $row[0]);
	}
}
info("DBs to *not* backup: ".join(',', @exclude_dbs));
my @dbs_to_backup_tmp;
foreach my $db (@dbs_to_backup){
	next if grep {$_ eq $db } @exclude_dbs;
	push(@dbs_to_backup_tmp, $db);
}

@dbs_to_backup = @dbs_to_backup_tmp;
info("DBs to be backed up: ".join(',', @dbs_to_backup));

info("Beginning backups");
foreach my $db (@dbs_to_backup){
	info(" DB: $db");
	# I don't know why placeholders don't work here:
	my $backup_dir = $backup_dir.$db.'/';
	mkdir($backup_dir) or abort ("Failed to create dir for '$db' (mkdir failed) : $!");

	my $sth = $dbh->prepare("show tables in $db");
	$sth->execute();
	while (my @row = $sth->fetchrow_array){
		my $table = $row[0];
		info("  Table: $table");
		my $backup_file = $backup_dir.$db.'--'.$table.'.sql.gz';

		my $cmd = join(" ", $mysqldump, @mysqldump_options, "'".$db."'", "'".$table."'", $gzip_pipe, '>', "'".$backup_file."'");
		info("    Command: $cmd");
		`$cmd`;
		my $retval = $?>>8;
		info("    Exited: $retval");
		info('    Wrote '. (stat($backup_file))[7] .' bytes');
		unless($retval == 0){
			abort("Backup failed (exiting $retval). See log file at $log_path");
			exit 2;
		}
	}
}
info("Backups done");


if($tar){
	tar($backups_dir,$backup_dirname);
	prune_tarballs($backups_dir, $retention) if $retention > 0 and $label =~ m/.+/;
}
unlock($lockfile);
info("Exiting OK");


# # # #
# # #
# #
#

sub tar{
	my $backups_dir = shift;
	my $backup_dirname = shift;
	info("Beginning tarring");
	chdir($backups_dir) or abort("Failed to cd to '$backups_dir' : $!");
	debug("chdir to $backups_dir");

	my $tarball = $backups_dir.$backup_dirname.$backup_tar_file_suffix;

	my $cmd = "$tar '$tarball' '$backup_dirname' 2>&1";
	info("Command: $cmd");
	open(my $pipe, "$cmd |") or abort ("Failed to open pipe to tar: $!");
	while (my $line = readline($pipe)){
		output($line);
	}
	my $retval = $?>>8;
	if($retval != 0){
		abort("Tar exited $retval");
	}
	info("Tar exited $retval");
	my $tarball_size = (stat($tarball))[7];
	info("Wrote tar file: $tarball ($tarball_size bytes)");

	# Just in case...
	unless($tarball_size > 4096){
		abort("Tarball only $tarball_size bytes. This looks wrong.");
}
	info("removing backup now that it's tarred");
	my $cmd = "rm -rf $backup_dirname 2>&1";
	info("Command: $cmd");
	foreach my $output(`$cmd`){
		output($cmd);
	}
	my $retval = $?>>8;
	if($retval != 0){
		abort("rm exited non-zero ($retval)");
	}
	info("Finished tarring");
}

sub prune_tarballs{
	my $backup_dir = shift;
	my $retention = shift;

	info("Starting pruning old backups");
	info("Deleting tarballs older than $retention days");
	chdir($backups_dir) or abort("Failed to cd to '$backups_dir'");
	my $cmd = "find $backup_dir -name \*$backup_tar_file_suffix -mtime +$retention -delete";
	info("Command: $cmd");
	my $retval = $?>>8;
	if($retval != 0){
		abort("find exited non-zero ($retval)");
	}
	info("Finished pruning old backups");
}

sub output{
	my $message = shift;
	chomp($message);
	print $log_fh "[".localtime()."]    $message\n";
}

sub info{
	my $message = shift;
	chomp($message);
	print $log_fh "[".localtime()."] $message\n";
	print STDERR 'INFO:  '.$message."\n" if $ENV{DEBUG};
}

sub abort{
	my $message = shift;
	my $dont_unlock = shift;
	chomp($message);
	print STDERR "ERROR: ".$message."\n";
	print STDERR "Aborting\n";
	unlock($lockfile, 1) unless $dont_unlock;
	exit 1;
}

sub debug{
	return unless $ENV{DEBUG};
	my $message = shift;
	chomp($message);
	print STDERR 'DEBUG: '.$message."\n";
}


sub get_backup_dirname{
	my ($dow,$y,$m,$d,$H,$M,$S) = (localtime(time()))[6,5,4,3,2,1,0];
	my @days = qw/mon tue wed thu fri sat sun mon/;
	$y+=1900;
	$m++;
	my $day = $days[$dow];
	my $dirname = sprintf("%04d-%02d-%02d_%02d:%02d:%02d_%s", $y,$m,$d,$H,$M,$S,$day);
	return $dirname;
}

sub write_lock{
	my $lockfile = shift;
	debug("Lockfile: $lockfile");
	if(-f $lockfile){
		open(my $fh, "<", $lockfile) or abort ("Failed to open lockfile '$lockfile' for reading when locking:$!");
		my $written_pid = readline($fh);
		close(fh);
		abort("Lockfile '$lockfile' already written-to by process '$written_pid'");
	}
	open(my $fh, ">", $lockfile) or abort ("Failed to open lockflile '$lockfile' for writing when locking: $!");
	print $fh $$;
	close($fh);
	sleep 1;
	open(my $fh, "<", $lockfile) or abort ("Failed to re-open lockfile '$lockfile' for reading when locking:$!");
	my $written_pid = readline($fh);
	close(fh);
	if($written_pid eq $$){
		return $written_pid;
	}
	abort("Lockfile '$lockfile' already written by process '$written_pid'");
}

sub unlock{
	my $lockfile = shift;
	my $called_by_abort = shift;
	open(my $fh, "<", $lockfile) or abort ("Failed to open lockfile '$lockfile' for reading when unlocking: $!");
	my $written_pid = readline($fh);
	close(fh);
	if($written_pid eq $$){
		info("Removing lock file $lockfile");
		unlink($lockfile);
		return 1;
	}
	if($called_by_abort){
		print "unlock() called by abort() failed: \"Lockfile '$lockfile' owned by another process: '$written_pid' (this is $$)\n";
		exit 255;
	}
	abort ("Lockfile '$lockfile' owned by another process: '$written_pid' (this is $$)");
}

sub open_logfile{
	my $logdir = shift;
	my $label = shift;

	unless(-d $logdir){
		abort("Logdir '$logdir' does not exist, or is not a directory");
	}

	debug("Logdir: $logdir, Label: $label");
	my @days = qw/mon tue wed thu fri sat sun mon/;
	my @months = qw/jan feb mar apr jun jul aug sep oct nov dec/;

	my ($dow,$y,$m,$d,$H,$M,$S) = (localtime(time()))[6,5,4,3,2,1,0];


	my ($y,$m,$d,$H,$dow) = (localtime(time()))[5,4,3,2,6];
	$y+=1900;
	my $day = $days[$dow];
	my $month = $months[$m];

	my $name;
	if(lc($label) eq 'daily'){
		$name = $day.'-daily';

	}elsif(lc($label) eq 'weekly'){
		$name = sprintf("%s-%02d-weekly", $month, $d);

	}elsif(lc($label) eq 'monthly'){
		$name = $month.'-monthly';

	}elsif(lc($label) eq 'hourly'){
		$name = sprintf("%02dh-hourly", $H);

	}elsif(lc($label) eq ''){
		$name = sprintf("%d-%02d-%02d_%02d:%02d:%02d", $y,$m+1,$d,$H,$M,$S);
	}else{
		$name = sprintf("%d-%02d-%02d_%02d:%02d:%02d_%s", $y,$m+1,$d,$H,$M,$S,$label);
	}

	my $path = $logdir.'/'.$name;
	$path =~ s#//#/#g;

	debug("Log name: $name");
	debug("Log path: $path");

	open(my $fh, ">", $path) or abort("Failed to open logfile '$path' : $!");
	return ($fh, $path);
}


sub usage{
print << "EOF";

$name - use mysqldump to backup a MySQL/MariaDB server

Usage:

    $name [options]


OPTIONS: 

Help:

  --help:         see this help

Credentials:

  By default, a defaults file is used to connect to the mysqld, the default 
  path for this is ~/.my.cnf

  Behaviour here is defined by mysqldump, this script does no sanitising.

  --defaults-file [path]

    Explicitly set the path to the defaults file

 --no-defaults-file
    
    Don't use a defaults file

  --username [user], --password [pass] and --hostname [host]

    Specify credentials when not using a defaults file. 

Output and logging:

  --no-log
    
    Don't write a log file; everything to STDOUT instead

  --log-dir:

    Directory in which to write a log. See 'label' below for info on log naming
    Default: $log_dir

Backing up:

  --mysqldump [path]

    path to the mysqldump binary. Default: $mysqldump

  --mysqldump-option

    Set a mysqldump option; call multiple times to set several options. To 
    avoid confusing the options parser, use the --opt=value syntax, eg:

      --mysqldump-option=--parallel=10

  --backups-dir [path]

    path to the directory in which to keep the backups.
    Default: $backups_dir

  --label

    A label to use for the backups. Influences the logfile's name, and normally
    one of "daily", "weekly" or "monthly" but only by convention. It may be any
    string

  --no-tar
    
    By defalt, the resulting directory is gzipped and tarred, setting --no-tar 
    causes the backup to be left as a directory. This also disables the pruning.

  --tar [command]

    Tar invocation; passed two arguments, the name of the tarball to create and
    the name of the directory to tar. Must, therefore, include the arguments. 
    Default: '$tar'

  --max-days [num]

    Number of days to keep backups for. When a label is set and tarring is not
    disabled, on succesful taking of a backup, all tarballs older than this 
    number of days with this label are deleted. No pruning is done if this is
    left unset.

  --lockfile

    Path to lockfile created to avoid having two processes running at the same 
    time. Default: $lockfile


LABELS

Labels are used to distinguish between backups of a different purpose, most 
likely those of a different period. When a label is set a subdirectory is 
created in the backups directory named for it, and so the max-days set only 
affects those.

Consider two cronsjobs:

  0 3  1 * * $name --label monthly --max-days 90
  0 17 * * * $name --label weekly --max-days 14

The first will be backed up into a directory /home/$name/monthly/, 
and the pruning will delete backups in the monthly/ directory older than 90
days.

The second will use a directory at /home/$name/weekly/, and the 
pruning will delete backups in weekly/ older than 14 days, but leave anything
in any other subdirectory.

If --label is unset, the backups just go into /home/$name, and 
pruning is disabled.


The logfile is also named in accordance with the labelling. The intention is 
to create a self-rotating log system. Daily logs are named for the day of the
week so there's always seven, for example.

The rules are:

  Label    |           Pattern                  |    Example
  ---------+------------------------------------+----------------------------
  daily    | <day name>-daily | mon-daily       |  tue-daily
  weekly   | <month name>-<day of month>-weekly |  feb-19-weekly
  monthly  | <month name>-monthly               |  feb-monthly
  hourly   | <hour>h-hourly                     |  17h-hourly
  empty    | y-m-d_H:M:S                        |  2018-02-19_17:04:12
  *        | y-m-d_H:M:S_<label>                |  2018-02-19_17:04:12_thing

That is, any non-empty string that's not daily, weekly, monthly or hourly will
just be appended verbatim to the end of the logfile's name.


DETECTING FAILURE

On succesful completion, the last line logged will be "Exiting OK" prefixed 
with a timestamp, and an exit value of 0.

Any problems result in a non-zero exit status and a log-line with an error
message in it.

EOF

exit 1;

}

